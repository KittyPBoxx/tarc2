const BERUS_HEAD_ID = 1
const BERUS_BODY_ID = 5

mapscripts Bridge_MapScripts {
    MAP_SCRIPT_ON_RESUME: Bridge_OnResume
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 1: BridgeIntro
    ]
}

script Bridge_OnResume {
    if (!flag(FLAG_BRIDGE_INTRO_DONE)) {
        setvar(VAR_TEMP_0, 1)
    }
    if (!flag(FLAG_CHOSE_STARTER)) {
        setvar(VAR_TEMP_1, 1)
        setflag(FLAG_TEMP_HIDE_FOLLOWER)
    }
    end
}

script BridgeIntro {
    if (var(VAR_TEMP_0) != 1) {
        end
    }

    delay(120)

    setvar(VAR_TEMP_0, 0)
    setflag(FLAG_TEMP_HIDE_FOLLOWER)
    setvar(VAR_TEMP_1, 1)

    lock 

    applymovement(OBJ_EVENT_ID_PLAYER, LookUp)

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("Decided to finally show up, did we?\p Is there a good reason you're six hours late?"))
    waitmessage
    removefieldmugshot

    msgbox("Tell the truth?", MSGBOX_YESNO)

    if (var(VAR_RESULT) == 1) {
        createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
        msgbox(format("Well, the truth is... I overslept."))
        removefieldmugshot
    } else {
        createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
        msgbox(format("Well, the truth is... I had to... er... give my train seat to a grandma...\pwho was in a wheelchair...\pand uh... also pregnant?\pThen she beat me up and stole my ticket."))
        removefieldmugshot
    }

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("Enough of the excuses! Just tell me your name so I can mark you down."))
    removefieldmugshot

    special(DoPlayerNamingScreen)
    setflag(FLAG_BRIDGE_INTRO_DONE)
    waitstate

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("{PLAYER}... ah! You must be Thya's child.\p Now there's a great trainer, plenty of deities after her contract."))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
    msgbox(format("She's so busy I haven't seen her in weeks."))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("An inspiration to us all! \pWell {PLAYER}, they say the apple doesn't fall far from the tree.\pBut that just means they spoil and never sprout."))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
    msgbox(format("...?"))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("I'm saying I cannot allow someone so tardy to take the exam."))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
    msgbox(format("But there's still an hour left! I'll be real quick."))
    removefieldmugshot

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("Not possible, even the best students take a few hours. \pAlthough....\pVery well, pick a Pokémon, you won't pass but I'll enjoy watching you suffer."))
    removefieldmugshot

    release  
    end
}

movement LookUp {
    face_up
}

// -------------------
// Berus (object interaction after intro)
// -------------------
script BerusNPC {
    lock

    if (defeated(TRAINER_BERUS))
    {
        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("You...\pYOU...\pYou may have beaten me, but...\pThe timer on the exam ran out during our battle...\pSo you still FAIL.\p Now if you'll excuse me I need to go and heal my poor doggo.")) 
        removefieldmugshot

        setflag(FLAG_HIDE_BERUS)
        removeobject(BERUS_HEAD_ID)
        removeobject(BERUS_BODY_ID)
    }
    elif (!flag(FLAG_CHOSE_STARTER)) {
        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("Choose a Pokémon from the bag before we proceed."))
        removefieldmugshot
    }
    elif (defeated(TRAINER_DEITY_TENJIN)) {
            
        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("Finally ready to admit defeat?\p What? You beat Tenjin? That's impossible!\pYou must have cheated somehow.\nI'll show you what I do to cheaters!")) 
        removefieldmugshot
        trainerbattle_single(TRAINER_BERUS, Debug_EventScript_Script_1_Text_Battle1_Intro, Debug_EventScript_Script_1_Text_Battle1_Defeat)

        special(GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON)
        {
            createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
            msgbox(format("You...\pYOU...\pYou may have beaten me, but...\pThe timer on the exam ran out during our battle...\pSo you still FAIL.\p Now if you'll excuse me I need to go and heal my poor doggo.")) 
            removefieldmugshot
            msgbox(format("Berus used a prayer.\nBerus has infinite heals."))

            setflag(FLAG_HIDE_BERUS)
            removeobject(BERUS_HEAD_ID)
            removeobject(BERUS_BODY_ID)
        }

    } 
    elif (!defeated(TRAINER_DEITY_TENJIN)) {
        
        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("Don't waste my time. You should focus on trying to pass the test...\pNot that you have a chance.")) 
        removefieldmugshot

    }
    else {

        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("I don't accept this. I do not accept that a delinquent like you could pass."))
        removefieldmugshot

    }

    release
    end
}

// -------------------
// Bag (starter choice)
// -------------------
script StarterBag {
    lock

    if (!flag(FLAG_CHOSE_STARTER)) {
        setflag(FLAG_CHOSE_STARTER)
        special(ChooseStarter)
	    waitstate

        createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
        msgbox(format("Wait, there's only one Pokémon in here..."))
        removefieldmugshot

        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("A good trainer makes the best choice in any situation.\pA great trainer makes sure there is always a good choice to make.\pYou're the last to pick so you get the leftovers."))
        removefieldmugshot

        createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
        msgbox(format("I guess that's my punishment for turning up late."))
        removefieldmugshot

        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("Oh, no. This is your punishment for turning up late."))
        removefieldmugshot

        msgbox(format("Berus used a prayer.\n{PLAYER} was cursed.\pYou will receive terrible luck for the rest of the test...\pAlso there might be some glitches.\nThis is the curse...\pAnd definitely not unfixed bugs."))

        clearflag(FLAG_TEMP_HIDE_FOLLOWER)
        setvar(VAR_TEMP_1, 0)
    }
    elif (flag(FLAG_HIDE_BERUS)) {

        createfieldmugshot(MUGSHOT_TEST, EMOTE_RAE)
        msgbox(format("He was in such a hurry he left his bag...\pWith his photo ID in it...\p"))
        removefieldmugshot

        msgbox("Get ditto to commit identity theft?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
            setvar(VAR_0x8008, SPECIES_MR_RIME)
            special(SetPlayerMonSpecies)
            msgbox(format("Ditto took the form of Berus.\p"))
            special(WarpToStairs) 
        }
    }
    else {
        createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
        msgbox(format("You already picked a Pokémon.\nStop touching my documents!")) 
        removefieldmugshot
    }

    release
    end
}

movement WalkLeft {
    walk_left
}

script PreventEarlyStart {
    lock

    createfieldmugshot(MUGSHOT_TEST, EMOTE_BERUS)
    msgbox(format("Choose a Pokémon from the bag before we proceed."))
    removefieldmugshot

    applymovement(OBJ_EVENT_ID_PLAYER, WalkLeft)
    waitmovement(0)

    release
    end
}


script BridgeWarpOrb {
    lock

    if (!flag(FLAG_UNLOCK_MANOR_WARP)) {
        setflag(FLAG_UNLOCK_MANOR_WARP)
        special(AutoSave)
        playfanfare(MUS_OBTAIN_BADGE) 
        msgbox(format("A new warp was unlocked!"))
        waitfanfare
    }

    msgbox(format("Use {SELECT_BUTTON} to warp to the next area."))

    release
    end
}
